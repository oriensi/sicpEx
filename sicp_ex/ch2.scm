;; 2.7
(define (make-center-interval m n)
  (make-interval (- m n) (+ m n)))
(define (make-interval a b)
  (cons a b))
(define (upper-bound interval)
  (if (> (car interval) (cdr interval))
      (car interval)
      (cdr interval)))
(define (lower-bound interval)
  (if (< (car interval) (cdr interval))
      (car interval)
      (cdr interval)))

(define (add-interval x y)
  (make-interval (+ (lower-bound x) (lower-bound y))
                 (+ (upper-bound x) (upper-bound y))))
(define (sub-interval x y)
  (make-interval (- (lower-bound x) (upper-bound y))
                 (- (upper-bound x) (lower-bound y))))

(define (mul-interval x y)
  (make-interval (* (lower-bound x) (lower-bound y))
                 (* (upper-bound x) (upper-bound y))))
(define (div-interval x y)
  (mul-interval x
                (make-interval (/ 1.0 (upper-bound y))
                               (/ 1.0 (lower-bound y)))))
;;
(define (part1 r1 r2)
  (div-interval (mul-interval r1 r2)
                (add-interval r1 r2)))
(define (part2 r1 r2)
  (let ((one (make-interval 1 1)))
    (div-interval one
                  (add-interval (div-interval one r1)
                                (div-interval one r2)))))


(define (append list1 list2)
  (if (null? list1)
      list2
      (cons (car list1) (append (cdr list1) list2))))
;; 2.17
(define (last-pair items)
  (cond ((null? items)       '())
        ((null? (cdr items))  items)
        (else (last-pair (cdr items)))))
;; 2.18
(define (reverse items)
  (define (reverse-item org obj)
    (if (null? org)
        obj
        (reverse-item (cdr org) (cons (car org) obj))))
  (reverse-item items '()))
;; 利用 append 构建形如
;; (append (append (cdr ()) (cons '())) (cons '()))
(define (reverse-d items)
  (if (null? items)
      '()
      (append (reverse-d (cdr items)) (cons (car items) '()))))

;; 找零
(define (count-change amount)
  (cc amount 5))
(define (cc amount kinds-of-coins)
  (cond ((= amount 0) 1)
        ((or (< amount 0) (= kinds-of-coins 0)) 0)
        (else (+ (cc amount (- kinds-of-coins 1))
                 (cc (- amount (first-denomination kinds-of-coins))
                     kinds-of-coins)))))
(define (first-denomination kinds-of-coins)
  (cond ((= kinds-of-coins 1) 1)
        ((= kinds-of-coins 2) 5)
        ((= kinds-of-coins 3) 10)
        ((= kinds-of-coins 4) 25)
        ((= kinds-of-coins 5) 50)))
(define (length list)
  (if (null? list)
      0
      (+ (length (cdr list)) 1)))
(define (get-item items index)
  (if (= index 0)
      (car items)
      (get-item (cdr items) (- index 1))))
(define (get-rindex items index)
  (get-item (reverse items) index))

(define (cc-2 amount coin-values)
  (cond ((= amount 0) 1)
        ((or (< amount 0) (null? coin-values)) 0)
        (else (+ (cc-2 amount (cdr coin-values))
                 (cc-2 (- amount (car coin-values)) coin-values)))))

;; 2.20 返回所有与起始元素奇偶性相同的元素
(define (if-append x y)
  (if (null? y)
      '()
      (if (x (car y))
          (cons (car y) (if-append x (cdr y)))
          (if-append x (cdr y)))))
(define (same-parity . w)
  (if (null? w)
      '()
      (let ((ou (remainder (car w) 2)))
        (if-append (lambda (x) (= ou (remainder x 2))) w))))

;; 2.21 map
(define (square-list items)
  (if (null? items)
      '()
      (cons (* (car items) (car items))
            (square-list (cdr items)))))
(define (square-list-map items)
  (map (lambda (x) (* x x)) items))

(define (count-leaves x)
  (cond ((null? x) 0)
        ((not (pair? x)) 1)
        (else (+ (count-leaves (car x))
                 (count-leaves (cdr x))))))

;; 2.25 取出7
(car (cdr (car (cdr (cdr (list 1 3 '(5 7) 9))))))
(car (car (list (list 7))))
(cadr (cadr (cadr (cadr (cadr (cadr (list 1 (list 2 (list 3 (list 4 (list 5 (list 6 7))))))))))))

;; 2.27 deep-reverse
(define (deep-reverse x)
  (cond ((null? x) '())
        ((pair? (car x)) (append (deep-reverse (cdr x)) (cons (deep-reverse (car x)) '())))
        (else (append (deep-reverse (cdr x)) (cons (car x) '())))))

;; 2.28 列出所有树叶
;; (define x (list (list 1 2) (list 3 4)))
(define (fringe x)
  (cond ((null? x) '())
        ((not (pair? x)) (list x))
        (else (append (fringe (car x)) (fringe (cdr x))))))

;; 2.29 二叉活动体, 长度 × 重量
;; list
(define (make-mobile left right)
  (list left right))
(define (make-branch length structure)
  (list length structure))
(define (left-branch tree)
  (car tree))
(define (right-branch tree)
  (car (cdr tree)))
(define (branch-length branch)
  (car branch))
(define (branch-structure branch)
  (car (cdr branch)))
;; 2.29 cons
;; (define (make-mobile left right)
;;   (cons left right))
;; (define (make-branch length structure)
;;   (cons length structure))
;; (define (right-branch tree)
;;   (cdr tree))
;; (define (branch-structure branch)
;;   (cdr branch))
(define (total-weight tree)
  (if (not (pair? tree))
      tree
      (+ (if (pair? (branch-structure (left-branch tree)))
             (total-weight (branch-structure (left-branch tree)))
             (branch-structure (left-branch tree)))
         (if (pair? (branch-structure (right-branch tree)))
             (total-weight (branch-structure (right-branch tree)))
             (branch-structure (right-branch tree))))))
(define (balence? tree)
  (if (not (pair? tree))
      #t
      (if
       (= (* (branch-length (left-branch tree))
             (total-weight (branch-structure (left-branch tree))))
          (* (branch-length (right-branch tree))
             (total-weight (branch-structure (right-branch tree)))))
       (and (balence? (branch-structure (left-branch tree)))
            (balence? (branch-structure (right-branch tree))))
       #f)))

;; 2.30
(define (square-tree tree)
  (map (lambda (sub-tree)
         (if (pair? sub-tree)
             (square-tree sub-tree)
             (* sub-tree sub-tree)))
       tree))

;; 2.31
(define (tree-map fun tree)
  (map (lambda (sub-tree)
         (if (pair? sub-tree)
             (tree-map fun sub-tree)
             (fun sub-tree)))
       tree))
(define (square-tree-map tree)
  (tree-map (lambda (x)
              (* x x))
            tree))

;; 2.32 生成子集
(define (subsets s)
  (if (null? s)
      (list '())
      (let ((rest (subsets (cdr s))))
        (append rest (map (lambda (x)
                            (cons (car s) x))
                          rest)))))
